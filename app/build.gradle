apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "io.fabric"
apply plugin: "com.google.gms.google-services"

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "tk.lorddarthart.githubuserfinder"
        minSdkVersion rootProject.min_sdk_version
        targetSdkVersion rootProject.target_sdk_version
        versionCode versionBuild
        versionName "${rootProject.versionMajor}.${rootProject.versionMinor}.${rootProject.versionPatch}-b${rootProject.versionBuild}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary true
    }
    signingConfigs {
        debug {}
        release {}
    }
    buildTypes {
        debug {
            versionNameSuffix "-dev"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
            versionNameSuffix "-release"
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith(".apk")) {
                def mFileName = "github-user-finder-v${versionName}.apk"
                outputFileName = mFileName
            }
        }
    }
    dataBinding.enabled = true
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Core
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.kotlin_version"
    implementation "androidx.core:core-ktx:$rootProject.androidx_core_version"

    // Base
    implementation "androidx.appcompat:appcompat:$rootProject.appcompat_version"
    implementation "androidx.legacy:legacy-support-v4:$rootProject.legacy_support_version"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraint_layout_version"
    implementation "androidx.fragment:fragment-ktx:$rootProject.androidx_fragment_version"
    implementation "com.google.android.material:material:$rootProject.material_version"

    // Android's Architecture Components
    implementation "androidx.lifecycle:lifecycle-viewmodel:$rootProject.androidx_architecture_components_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$rootProject.androidx_architecture_components_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.androidx_architecture_components_version"
    kapt "androidx.lifecycle:lifecycle-common-java8:$rootProject.androidx_architecture_components_version"

    // Data Binding Library
    kapt "com.android.databinding:compiler:$rootProject.databinding_library_version"

    // Kotlin support
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.kotlin_version"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$rootProject.coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.coroutines_version"

    // Firebase Libraries
    implementation "com.google.firebase:firebase-core:$rootProject.firebase_core_version"
    implementation "com.crashlytics.sdk.android:crashlytics:$rootProject.firebase_crashlytics_version"
    implementation "com.google.firebase:firebase-auth:$rootProject.firebase_auth_version"

    // Google Play Services Auth
    implementation "com.google.android.gms:play-services-auth:$rootProject.play_services_auth_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofit_version"

    // OkHttp3 Logging interceptor for logging during Http request.
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okhttp_logging_interceptor_version"

    // Picasso
    implementation "com.squareup.picasso:picasso:$rootProject.picasso_version"

    // CircleImageView
    implementation "de.hdodenhof:circleimageview:$rootProject.circle_image_view_version"
}
